10 REM TRADUCTOR DE ABACOS
20 REM Joaquin Ferrero, agosto 2023
30 REM 
100 AL=7:AN=12
105 DIM ALAMBRE$(AN),ABACO$(AL*AN)
107 RESTORE 
110 FOR A=1 TO AL
120 READ ALAMBRE$
125 REM Vamos acumulando alambres
130 ABACO$((A-1)*AN+1)=ALAMBRE$
140 NEXT A
180 DIM N$(AL+2):N$=" .   .   "
200 GOSUB 1000
500 PRINT "Resultado: ";N$
510 END 
1000 REM TRADUCTOR
1010 REM Vamos leyendo los alambres
1020 REM y almacenando los digitos.
1060 REM El resultado queda en N$
1100 IN=1
1110 FOR A=1 TO AL
1120 ALAMBRE$=ABACO$((A-1)*AN+1,A*AN)
1130 N=0:FOR C=1 TO AN
1140 IF ALAMBRE$(C,C)="O" THEN GOTO 1200
1150 POP :GOTO 1210
1200 N=N+1:NEXT C
1210 N$(IN,IN)=STR$(N)
1220 IN=IN+1
1230 IF (IN=2)+(IN=6) THEN IN=IN+1
1400 NEXT A
1500 RETURN 
1999 REM Abaco de ejemplo
2000 DATA O---OOOOOOOO
2010 DATA OOO---OOOOOO
2020 DATA ---OOOOOOOOO
2030 DATA OO---OOOOOOO
2040 DATA OOOOOOO---OO
2050 DATA OOOOOOOOO---
2060 DATA ---OOOOOOOOO
