10 REM 
20 REM Calculador de numero de columna
30 REM Dada una columna de hoja de
40 REM calculo, mostrar el numero de
50 REM esa columna.
60 REM 
70 REM Joaquin Ferrero, agosto 2023
80 REM 
100 REM Programa
110 REM 
120 DIM COL$(10),CHAR$(1)
130 RESTORE 500
140 READ COL$
150 IF COL$="-1" THEN 490
170 GOSUB 1000:REM CALCULAR COLUMNA
180 ? COL$,N
200 GOTO 140
490 END 
500 REM Pruebas
510 DATA A
511 DATA D
512 DATA AA
513 DATA ZZ
514 DATA E15784
515 DATA CA
516 DATA 
517 DATA CAFE
518 DATA $ABAD$4
519 DATA ABAD54
520 DATA ABAD
521 DATA MOUREDEV
590 DATA -1
1000 REM Numero de columna
1010 N=0
1020 L=LEN(COL$):IF L=0 THEN RETURN 
1030 FOR J=1 TO L
1040 CHAR$=COL$(J,J)
1050 A=ASC(CHAR$)
1055 IF CHAR$<"A" OR CHAR$>"Z" THEN 1070
1060 N=N*26+A-ASC("A")+1
1070 NEXT J
1999 RETURN 
9998 LIST "H1:EXCELCOL.LST":END 
9999 SAVE "H1:EXCELCOL.BAS":END 
