10 REM GENERADOR DE PASSWORDS
20 REM Joaquin Ferrero
30 ? "LONGITUD DEL PASSWORD:";:INPUT L
32 IF L<8 OR L>16 THEN GOTO 30
35 DIM M$(1),N$(1),S$(1)
40 ? "MAYUSCULAS [S/N]";:INPUT M$
50 ? "NUMEROS [S/N]";:INPUT N$
60 ? "SIMBOLOS [S/N]";:INPUT S$
90 REM 
100 REM GRUPOS DE CARACTERES
105 REM 
110 DIM ALFAS$(26)
120 ALFAS$="abcdefghijklmnopqrstuvwxyz"
130 DIM MAYUS$(26)
140 MAYUS$="ABCDEFGHIJKLMNOPQRSTUVWXYZ"
150 DIM NUM$(10)
160 NUM$="0123456789"
170 DIM SIMB$(17)
180 SIMB$="+-*/()&!%='[],.;:"
195 REM 
200 REM CARACTERES POSIBLES
205 REM 
210 DIM CARACTERES$(26+26+10+17)
220 CARACTERES$=ALFAS$
250 IF M$="S" THEN CARACTERES$(LEN(CARACTERES$)+1)=MAYUS$
260 IF N$="S" THEN CARACTERES$(LEN(CARACTERES$)+1)=NUM$
270 IF S$="S" THEN CARACTERES$(LEN(CARACTERES$)+1)=SIMB$
295 REM 
300 REM GENERACION
305 REM 
310 LARGO=LEN(CARACTERES$)
320 DIM PASSWORD$(L)
330 FOR I=1 TO L
340 X=INT(LARGO*RND(0))+1
350 PASSWORD$(I,I)=CARACTERES$(X,X)
360 NEXT I
370 ? PASSWORD$
